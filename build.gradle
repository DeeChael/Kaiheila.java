plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'net.deechael'
version '1.00.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'org.json:json:20211205'
    implementation 'com.google.code.gson:gson:2.9.0'
    testImplementation 'junit:junit:4.13.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.1'
    implementation 'ch.qos.logback:logback-classic:1.2.10'
    implementation 'commons-lang:commons-lang:2.6'
    implementation 'org.yaml:snakeyaml:1.30'
    implementation 'com.google.guava:guava:31.0.1-jre'
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}
java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.encoding = "UTF-8"
    options.addStringOption("charset", "UTF-8")
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'khl'
            from components.java
            pom {
                name = 'khl'
                description = 'Java sdk for Kaiheila bot development'
                url = 'https://www.github.com/DeeChael/Kaiheila.java'
                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'DeeChael'
                        name = 'DeeChael'
                        email = 'DeeChael@outlook.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://www.github.com/DeeChael/Kaiheila.java.git'
                    developerConnection = 'scm:git:ssh://www.github.com/DeeChael/Kaiheila.java.git'
                    url = 'https://www.github.com/DeeChael/Kaiheila.java'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            if (project.version.toString().endsWith("-SNAPSHOT")) {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            } else {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
            credentials {
                username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}

signing {
    sign(publishing.publications.mavenJava)
}